// web-view 1.0.0 : sky 3.0.0-2
// https://raw.githubusercontent.com/omega-gg/Sky-runtime/refs/heads/master/script/player.sky
//=================================================================================================
/*
    Copyright (C) 2015-2020 Sky kit authors. <http://omega.gg/Sky>

    Author: Benjamin Arnaud. <http://bunjee.me> <bunjee@omega.gg>

    This file is part of the Sky kit runtime.

    - GNU Lesser General Public License Usage:
    This file may be used under the terms of the GNU Lesser General Public License version 3 as
    published by the Free Software Foundation and appearing in the LICENSE.md file included in the
    packaging of this file. Please review the following information to ensure the GNU Lesser
    General Public License requirements will be met: https://www.gnu.org/licenses/lgpl.html.

    - Private License Usage:
    Sky kit licensees holding valid private licenses may use this file in accordance with the
    private license agreement provided with the Software or, alternatively, in accordance with the
    terms contained in written agreement between you and Sky kit authors. For further information
    contact us at contact@omega.gg.
*/
//=================================================================================================

import QtQuick   2.0
import QtWebView 2.0
import Sky       1.0

Item
{
    //---------------------------------------------------------------------------------------------
    // Properties
    //---------------------------------------------------------------------------------------------

    /* read */ property bool isReady: (webView.url != "")

    property int topMargin: gui.topMargin

    property int timeout: 30000

    //---------------------------------------------------------------------------------------------
    // Aliases
    //---------------------------------------------------------------------------------------------

    property alias webView: webView

    property alias loader: loader

    //---------------------------------------------------------------------------------------------
    // Functions
    //---------------------------------------------------------------------------------------------
    // Interface

    function load(url)
    {
        if (webView.url == url) return true;

        webView.url = url;

        // NOTE: If we don't call this, webView.loading is set to false.
        sk.processEvents();

        var start = Date.now();

        while (webView.loading && webView.loadProgress != 100
               &&
               Date.now() - start < timeout) sk.processEvents();

        if (webView.loadProgress != 100)
        {
            console.debug("web-view: Failed to load " + url);

            return false;
        }

        return true;
    }

    //---------------------------------------------------------------------------------------------
    // Events

    function onLoader()
    {
        return loader;
    }

    function onConsole(list)
    {
        var length = list.length;

        var command = list[0];

        if (command == "load")
        {
            if (length != 2) return false;

            load(list[1]);

            return true;
        }

        return false;
    }

    function onHelp()
    {
        return "Welcome to Sky web-view " + sk.versionSky + "\n" +
               "\n" +
               "console:\n" +
               "> load <url>    load the url";
    }

    //---------------------------------------------------------------------------------------------
    // Children
    //---------------------------------------------------------------------------------------------

    WebView
    {
        id: webView

        anchors.fill: parent

        anchors.topMargin: topMargin

        visible: isReady
    }

    Loader
    {
        id: loader

        anchors.fill: parent

        sourceComponent: (isReady) ? null : componentDefault
    }

    Component
    {
        id: componentDefault

        Item
        {
            TextBase
            {
                anchors.centerIn: parent

                text: qsTr("Welcome to Sky web-view")

                font.pixelSize: st.dp32
            }
        }
    }
}
